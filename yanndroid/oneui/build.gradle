plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = GROUP_ID
            artifactId = ARTIFACT_ID
            version = VERSION_NAME
            artifact("$rootDir/yanndroid/oneui/build/outputs/aar/oneui-release.aar")

            pom {
                name = GROUP_ID
                description = POM_DESCRIPTION
                url = POM_URL
                licenses {
                    license {
                        name = POM_LICENSE_NAME
                        url = POM_LICENSE_URL
                    }
                }
                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                        email = POM_DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                    url = POM_SCM_URL
                }
            }

        }
    }
}


File propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(propertiesFile.newDataInputStream())
    ext["signing.keyId"] = properties.getProperty('signing.keyId')
    ext["signing.password"] = properties.getProperty('signing.password')
    ext["signing.secretKeyRingFile"] = properties.getProperty('signing.secretKeyRingFile')
} else {
    ext["signing.keyId"] = "$System.env.SIGNING_KEYID"
    ext["signing.password"] = "$System.env.SIGNING_PASSWORD"
    ext["signing.secretKeyRingFile"] = "$System.env.SIGNING_KEYFILE"
}

signing {
    sign publishing.publications
}

//gradlew publishToSonatype closeSonatypeStagingRepository
//gradlew publishToSonatype closeAndReleaseSonatypeStagingRepository


android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode = VERSION_CODE
        versionName = VERSION_NAME

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "sesl_font_family_regular", "sec-roboto-light"
        }
        debug {
            resValue "string", "sesl_font_family_regular", "sans-serif"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    /*project.gradle.addBuildListener(new BuildListener() {
        @Override
        void settingsEvaluated(Settings settings) {}

        @Override
        void projectsLoaded(Gradle gradle) {}

        @Override
        void projectsEvaluated(Gradle gradle) {
            print("change to sec-roboto-light")
            File stringFile = new File("${projectDir}/src/main/res/values/strings.xml")
            stringFile.write(stringFile.getText('UTF-8').replace("<string name=\"sesl_font_family_regular\" translatable=\"false\">sans-serif-thin</string>", "<string name=\"sesl_font_family_regular\" translatable=\"false\">sec-roboto-light</string>"), 'UTF-8')
        }

        @Override
        void buildFinished(BuildResult result) {
            print("change to sans-serif-thin")
            File stringFile = new File("${projectDir}/src/main/res/values/strings.xml")
            stringFile.write(stringFile.getText('UTF-8').replace("<string name=\"sesl_font_family_regular\" translatable=\"false\">sec-roboto-light</string>", "<string name=\"sesl_font_family_regular\" translatable=\"false\">sans-serif-thin</string>"), 'UTF-8')
        }
    })*/
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
